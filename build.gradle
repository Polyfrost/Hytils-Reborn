buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }

        flatDir dirs: 'libs'
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:86b2392"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion
    makeObfSourceJar = false
    clientJvmArgs += "-Dfml.coreMods.load=club.sk1er.hytilities.tweaker.HytilitiesTweaker"
}

dependencies {
    provided files("libs/modcore.jar")
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info", "LICENSE.txt"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes "FMLCorePlugin": "club.sk1er.hytilities.tweaker.HytilitiesTweaker",
                "ModSide": "CLIENT",
                'FMLAT': 'hytilities_at.cfg',
                "FMLCorePluginContainsFMLMod": "Yes, yes it does"
    }
}
